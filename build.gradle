buildscript {
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.0'
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'
    /* does not work as of gradle 4.3.1: https://github.com/gradle/gradle/issues/3210
       TODO fix it after gradle upgrade
    id 'org.junit.platform.gradle.plugin' version '1.0.0' */
}


allprojects {

    group = 'ru.snm.tutorial.java_8_lambdas'
    version = '1.0.0-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://dl.bintray.com/jetbrains/spek" }
        maven { url "https://plugins.gradle.org/m2/" }
    }


    apply plugin: "io.spring.dependency-management"

    dependencyManagement {

        dependencies {
            dependencySet( group: 'org.junit.jupiter', version: '5.0.2' ) {
                entry 'junit-jupiter-api'
                entry 'junit-jupiter-engine'
                entry 'junit-jupiter-params'
            }

            dependencySet( group: 'org.jetbrains.kotlin', version: '1.1.61' ) {
                entry 'kotlin-stdlib'
                entry 'kotlin-reflect'
            }

            dependencySet( group: 'org.jetbrains.spek', version: '1.1.5' ) {
                entry( 'spek-api' ) {
                    exclude group: 'org.jetbrains.kotlin'
                }
                entry( 'spek-junit-platform-engine' ) {
                    exclude group: 'org.junit.platform'
                    exclude group: 'org.jetbrains.kotlin'
                }
            }
        }
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'kotlin'
    apply plugin: 'org.junit.platform.gradle.plugin'

    dependencies {

        compile 'org.jetbrains.kotlin:kotlin-stdlib'

        testCompile (
                'org.jetbrains.spek:spek-api',
                'org.jetbrains.kotlin:kotlin-reflect',

                'org.junit.jupiter:junit-jupiter-api',
                'org.junit.jupiter:junit-jupiter-params',
        )

        testRuntime(
                'org.jetbrains.spek:spek-junit-platform-engine',

                'org.junit.jupiter:junit-jupiter-engine',
        )
    }

    junitPlatform {
        filters {
            engines {
                include 'spek'
                include 'junit-jupiter'
            }
        }
    }
    
}

